apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = PROJECT_GROUP
version = PROJECT_VERSION

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: mavenLocal().getUrl())
                pom.groupId = PROJECT_GROUP
                pom.artifactId = POM_ARTIFACT_ID
                pom.version = PROJECT_VERSION
            }
        }
    }
}

def getBintrayUsername() {
    return hasProperty('BINTRAY_USERNAME') ? BINTRAY_USERNAME : ""
}

def getBintrayApiKey() {
    return hasProperty('BINTRAY_API_KEY') ? BINTRAY_API_KEY : ""
}

def getGPGPassword() {
    return hasProperty('BINTRAY_GPG_PASSWORD') ? BINTRAY_GPG_PASSWORD : ""
}

def getSonatypeUserName() {
    return hasProperty('SONATYPE_USERNAME') ? SONATYPE_USERNAME : ""
}

def getSonatypePassword() {
    return hasProperty('SONATYPE_PASSWORD') ? SONATYPE_PASSWORD : ""
}

bintray {
    user = getBintrayUsername()
    key = getBintrayApiKey()

    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = POM_NAME
        desc = POM_DESCRIPTION
        websiteUrl = POM_URL
        vcsUrl = POM_GIT_URL
        licenses = [POM_LICENCE_NAME]
        publish = true
        publicDownloadNumbers = true
        version {
            desc = "${POM_NAME} ${PROJECT_VERSION}."
            gpg {
                sign = true
                passphrase = getGPGPassword()
            }
            mavenCentralSync {
                sync = true
                user = getSonatypeUserName()
                password = getSonatypePassword()
                close = '1'
            }
        }
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                name POM_NAME
                description POM_DESCRIPTION
                url POM_URL

                packaging POM_PACKAGING
                groupId PROJECT_GROUP
                artifactId POM_ARTIFACT_ID
                version PROJECT_VERSION

                licenses {
                    license {
                        name POM_LICENCE_NAME
                        url POM_LICENCE_URL
                    }
                }
                developers {
                    developer {
                        id POM_DEVELOPER_ID
                        name POM_DEVELOPER_ID
                        email POM_DEVELOPER_EMAIL
                    }
                }
                scm {
                    url POM_URL
                    connection POM_GIT_URL
                    developerConnection POM_GIT_URL
                }
            }
        }
    }
}

if (project.getPlugins().hasPlugin("com.android.library")) {
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.source
    }
    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.source
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        failOnError false
    }
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
} else {
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}